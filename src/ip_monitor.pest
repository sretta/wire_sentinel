event_type = _{ "[" ~ "ADDR" ~ "]" }
deletion = { "Deleted" }
deletion_block = _{ (deletion ~ WHITE_SPACE)? }
index = _{ ASCII_DIGIT+ ~ ":" }
interface = _{ ASCII_ALPHANUMERIC* }
ipv4_network_address = _{ (ASCII_DIGIT{1,3} ~ "."){3} ~ ASCII_DIGIT{1,3} }
brd_block = _{ "brd" ~ WHITE_SPACE ~ ipv4_network_address }
ipv4_netmask = _{ "/" ~ ASCII_DIGIT{1,2} }
ipv4_address = { (ASCII_DIGIT{1,3} ~ "."){3} ~ ASCII_DIGIT{1,3} }
ipv4_address_brd_block = _{ "inet" ~ WHITE_SPACE ~ ipv4_address ~ ipv4_netmask ~ WHITE_SPACE ~ brd_block }
ipv4_address_block = _{ "inet" ~ WHITE_SPACE ~ ipv4_address ~ ipv4_netmask }
ipv6_netmask = _{ "/" ~ ASCII_DIGIT{1,3} }
ipv6_address = { (":"{0,2} ~ ASCII_HEX_DIGIT{1,4} ~ ":"{0,2})+ }
ipv6_address_block = _{ "inet6" ~ WHITE_SPACE ~ ipv6_address ~ ipv6_netmask }
ip_address_block = _{ ipv6_address_block | ipv4_address_brd_block | ipv4_address_block }
global_scope = { "global" }
link_scope = { "link" }
scope = _{ global_scope | link_scope }
scope_block = _{ "scope" ~ WHITE_SPACE ~ scope }
keyword = _{ "dynamic" | "deprecated" | "tentative" }
keyword_block = _{ (keyword ~ WHITE_SPACE)* }
route_block = _{ "noprefixroute"? ~ interface? }

address_line = _{ event_type* ~ deletion_block ~ index ~ WHITE_SPACE ~ interface ~ WHITE_SPACE+ ~ ip_address_block ~ WHITE_SPACE ~ scope_block ~ WHITE_SPACE ~ keyword_block ~ route_block}

lifetime = _{"forever" | (ASCII_DIGIT+ ~ "sec")}
lifetime_line = _{ WHITE_SPACE+ ~ "valid_lft" ~ WHITE_SPACE ~ lifetime ~ WHITE_SPACE ~ "preferred_lft" ~ WHITE_SPACE ~ lifetime }

line = { address_line | lifetime_line }
